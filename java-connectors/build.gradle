plugins {
    id 'com.diffplug.spotless' version "6.25.0"
    id 'java'
    id 'java-library'
}

allprojects {
    group = "io.lenses.streamreactor"
    version = "8.2.0-SNAPSHOT"
    description = "stream-reactor"

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'com.diffplug.spotless'

    java {
        setSourceCompatibility(JavaVersion.VERSION_11)
        setTargetCompatibility(JavaVersion.VERSION_11)
    }

    ext {
        //DEPENDENCY VERSIONS
        lombokVersion = '1.18.32'
        kafkaVersion = '3.7.0'
        logbackVersion = '1.4.14'
        jUnitVersion = '5.9.1'
        mockitoJupiterVersion = '5.10.0'
        apacheToConfluentVersionAxis = ["2.8.1": "6.2.2", "3.3.0": "7.3.1"]
        caffeineVersion = '3.1.8'
        cyclopsVersion = '10.4.1'
        bouncyCastleVersion = "1.78.1"

        //Other Manifest Info
        mainClassName = ''
        gitCommitHash = ("git rev-parse HEAD").execute().text.trim()
        gitTag = ("git describe --abbrev=0 --tags").execute().text.trim()
        gitRepo = ("git remote get-url origin").execute().text.trim()

        //for jar building
        rootRelease = "${project.rootDir}/release/"
        versionDir = "${rootRelease}/${project.description}-${project.version}"
        confDir = "${versionDir}/conf"
        libsDir = "${rootRelease}"
    }

    repositories {
        mavenCentral()
        maven {
            url "https://packages.confluent.io/maven/"
        }
    }

    dependencies {
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion

        compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

        implementation group: 'com.oath.cyclops', name: 'cyclops', version: cyclopsVersion
        implementation group: 'com.oath.cyclops', name: 'cyclops-pure', version: cyclopsVersion

        testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoJupiterVersion
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: mockitoJupiterVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: jUnitVersion
        testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.26.3'
    }

    test {
        useJUnitPlatform()
        maxHeapSize = '1G'
        testLogging {
            events "passed"
        }
    }

    jar {
        manifest {
            attributes(
                    "StreamReactor-Version": project.version,
                    "Kafka-Version": kafkaVersion,
                    "Created-By": "Lenses",
                    "Created-At": new Date().format("YYYYMMDDHHmm"),
                    "Git-Repo": gitRepo,
                    "Git-Commit-Hash": gitCommitHash,
                    "Git-Tag": gitTag,
                    "StreamReactor-Docs": "https://docs.lenses.io/connectors/"
            )
        }
    }

    task collectDependencies(type: Copy) {
        from configurations.runtimeClasspath
        into "${libsDir}/${project.name}"
        exclude {
            it.file.name.contains('log4j-core') ||
                    it.file.name.startsWith('kafka-') ||
                    it.file.name.startsWith('zookeeper-')
        }
    }

    task copyModuleJar(type: Copy, dependsOn: jar) {
        from jar.archiveFile
        into "${libsDir}/${project.name}"
        rename { filename ->
            def artifactVersion = gitTag?.trim() ? gitTag : project.version
            "${project.name}-${artifactVersion}.jar"
        }
    }

    task collectArtifacts(dependsOn: [test, collectDependencies, copyModuleJar])

    spotless {
        format 'misc', {
            target '.gitattributes', '.gitignore'
            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
        groovyGradle {
            target '**/*.gradle'
            importOrder()
            trimTrailingWhitespace()
            removeSemicolons()
            greclipse()
            indentWithSpaces(4)
        }
        java {
            targetExclude '**/io/lenses/kcql/antlr4/**/*.java'
            toggleOffOn()
            eclipse('4.30').configFile("${rootDir}/config/Lenses_eclipse.xml")
            licenseHeaderFile(rootProject.file("HEADER.txt"))
            custom 'noWildcardImports', {
                if (it.contains('.*;\n')) {
                    throw new Error('No wildcard imports allowed')
                }
            }
            bumpThisNumberIfACustomStepChanges(1)
        }
    }
}

task prepareRelease(dependsOn: subprojects.collectArtifacts)

task testModuleList() {
    def nonTestModules = ["java-reactor"]
    def modulesFile = new File("gradle-test-modules.txt")
    modulesFile.delete()
    modulesFile.createNewFile()
    def modulesBuilder = new StringBuilder("[")
    allprojects.name.stream()
            .filter { moduleName -> !nonTestModules.contains(moduleName) }
            .forEach { moduleName -> modulesBuilder.append("\"" + moduleName + "\",") }
    modulesBuilder.deleteCharAt(modulesBuilder.lastIndexOf(",")).append("]")
    modulesFile.append(modulesBuilder)
}

task releaseModuleList() {
    def nonReleaseModules = [
            "java-reactor",
            "test-utils",
            "kafka-connect-cloud-common",
            "kafka-connect-common",
            "kafka-connect-gcp-common",
            "kafka-connect-query-language",
            "kafka-connect-sink-reporting"
    ]
    def modulesFile = new File("gradle-modules.txt")
    modulesFile.delete()
    modulesFile.createNewFile()
    def modulesBuilder = new StringBuilder("[")
    allprojects.name.stream()
            .filter { moduleName -> !nonReleaseModules.contains(moduleName) }
            .forEach { moduleName -> modulesBuilder.append("\"" + moduleName + "\",") }
    modulesBuilder.deleteCharAt(modulesBuilder.lastIndexOf(",")).append("]")
    modulesFile.append(modulesBuilder)
}

task prepareModuleList() {
    dependsOn testModuleList
    dependsOn releaseModuleList
}