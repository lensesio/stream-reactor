buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
        classpath 'net.researchgate:gradle-release:2.3.4'
    }
}

apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'net.researchgate.release'
apply plugin: 'antlr'

allprojects {
    group = 'com.datamountaineer'
    version = version
    description = "kcql"
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        antlrVersion = "4.5.3"
        confluentVersion = "2.0.1"
        guavaVersion = "19.0"
        junitVersion = "4.12"
        mockitoVersion = "1.10.19"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://packages.confluent.io/maven/" }
        maven { url "http://repo.typesafe.com/typesafe/releases/" }
    }

    dependencies {
        antlr "org.antlr:antlr4:$antlrVersion"
        testCompile "org.antlr:antlr4-maven-plugin:$antlrVersion"
        compile "org.antlr:antlr4-runtime:$antlrVersion"
        testCompile "org.testng:testng:6.9.9"

    }

    generateGrammarSource {
        arguments += [ "-package" , "com.datamountaineer.connector.config.antlr4"]
        outputDirectory = new File("${project.projectDir}/src/main/java/com/datamountaineer/connector/config/antlr4")
    }

    test {
        maxParallelForks = 1
        minHeapSize '256m'
        maxHeapSize '2048m'
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        baseName = "test-${project.archivesBaseName}"
        from sourceSets.test.output
    }

    configurations {
        tests
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    tasks.withType(Tar) {
        compression Compression.GZIP
        extension = 'tgz'
    }


    artifacts {
        archives javadocJar, sourcesJar
    }

    compileJava.dependsOn generateGrammarSource

    task compile(dependsOn: 'compileJava')
    task fatJar(dependsOn: ['test', 'shadowJar'])
    task fatJarNoTest(dependsOn: 'shadowJar')

    signing {
        sign configurations.archives
    }

    // OSSRH publication
    if (project.hasProperty('release')) {
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        artifactId "kcql"
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/datamountaineer/kafka-connector-query-langauge'

                        scm {
                            connection 'scm:git:https://github.com/datamountaineer/kafka-connector-query-langauge.git'
                            developerConnection 'scm:git:git@github.com:datamountaineer/kafka-connector-query-langauge.git'
                            url 'https://github.com/datamountaineer/kafka-connector-query-langauge.git'
                        }

                        licenses {
                            license {
                                name 'Apache License 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.html'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'andrewstevenson'
                                name = 'Andrew Stevenson'
                                email = 'andrew@datamountaineer.com'
                            }
                            developer {
                                id = 'stheppi'
                                name = 'Stefan Bocutiu'
                                email = 'stefan@datamountaineer.com'
                            }
                            developer {
                                id = 'Antwnis'
                                name = 'Antonios Chalkiopoulos'
                                email = 'antonios@datamountaineer.com'
                            }
                        }
                    }
                }
            }
        }

        nexusStaging {
            packageGroup = project.getGroup()
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}

