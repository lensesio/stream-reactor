plugins {
    id "com.github.spotbugs" version "2.0.0"
}

// BEGIN ALL PROJECTS //
allprojects {
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

def withoutKafka = {
    exclude group: 'org.apache.kafka', module: 'connect-api'
    exclude group: 'org.apache.kafka', module: 'connect-transforms'
    exclude group: 'org.apache.kafka', module: 'kafka-clients'
}

// END ALL PROJECTS

project.ext {
    apacheHttpClientVersion = '4.5.6'
    avroVersion = '1.8.1'
    debeziumVersion = '0.6.1'
    googleCloudVersion = '1.79.0'
    googleAuthVersion = '0.9.0'
    googleCloudGsonVersion = '2.8.5'
    ioConfluentVersion = '5.5.1'
    junitVersion = '4.12'
    kafkaVersion = '2.5.0'
    kafkaScalaVersion = '2.12' // For integration testing only
    mockitoVersion = '3.2.4'
    slf4jVersion = '1.6.1'
}

// BEGIN SUBPROJECTS //
subprojects { subproject ->

    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    jar.baseName = subproject.name

    [compileJava, compileTestJava].each {
        it.options.compilerArgs << '-Xlint:unchecked'
    }

    checkstyle {
        configFile = file("${rootDir}/config/checkstyle/google_checks.xml")
        toolVersion = '6.18'
    }

    spotbugsMain {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    signing {
        sign configurations.archives
        required {
            gradle.taskGraph.hasTask('uploadArchives')
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                    authentication(userName: findProperty('ossrhUsername') ?: '', password: findProperty('ossrhPassword') ?: '')
                }

                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                    authentication(userName: findProperty('ossrhUsername') ?: '', password: findProperty('ossrhPassword') ?: '')
                }

                pom.project {

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    scm {
                        connection 'scm:git:git://github.com/wepay/kafka-connect-bigquery.git'
                        developerConnection 'scm:git:ssh://github.com:wepay/kafka-connect-bigquery.git'
                        url 'https://github.com/wepay/kafka-connect-bigquery'
                    }

                    developers {
                        developer {
                            id 'C0urante'
                            name 'Chris Egerton'
                            email 'fearthecellos@gmail.comw'
                        }

                        developer {
                            id 'moirat'
                            name 'Moira Tagle'
                            email 'moirat@wepay.com'
                        }
                    }
                }
            }
        }
    }
}
// END SUBPROJECTS

// BEGIN INDIVIDUAL PROJECTS
project(':kcbq-connector') {
    apply plugin: 'jacoco'

    jar {
        manifest {
            attributes  'Implementation-Title': 'Kafka Connect BigQuery Connector',
                        'Implementation-Version': version

        }
    }

    repositories {
        mavenCentral()
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output
                runtimeClasspath += main.output
                srcDir file('src/integration-test/java')
            }
            resources.srcDir file('src/integration-test/resources')
        }
    }

    test {
        useJUnit {
            // Exclude embedded integration tests from normal testing since they require BigQuery
            // credentials and can take a while
            excludeCategories 'org.apache.kafka.test.IntegrationTest'
        }
    }

    task embeddedIntegrationTest(type: Test) {
        useJUnit {
            includeCategories 'org.apache.kafka.test.IntegrationTest'
        }

        // Enable logging for integration tests
        testLogging {
            outputs.upToDateWhen {false}
            showStandardStreams = true
        }
    }

    task integrationTestPrep() {
        dependsOn 'integrationTestTablePrep'
        dependsOn 'integrationTestBucketPrep'
    }

    task integrationTestTablePrep(type: JavaExec) {
        main = 'com.wepay.kafka.connect.bigquery.it.utils.TableClearer'
        classpath = sourceSets.integrationTest.runtimeClasspath
        args findProperty('kcbq_test_keyfile') ?: ''
        args findProperty('kcbq_test_project') ?: ''
        args findProperty('kcbq_test_dataset') ?: ''
        args findProperty('kcbq_test_keysource') ?: ''
        if (findProperty('kcbq_test_tables') != null)
            args findProperty('kcbq_test_tables').split(' ')
    }

    task integrationTestBucketPrep(type: JavaExec) {
        main = 'com.wepay.kafka.connect.bigquery.it.utils.BucketClearer'
        classpath = sourceSets.integrationTest.runtimeClasspath
        args findProperty('kcbq_test_keyfile') ?: ''
        args findProperty('kcbq_test_project') ?: ''
        args findProperty('kcbq_test_bucket') ?: ''
        args findProperty('kcbq_test_keysource') ?: ''
    }

    task integrationTest(type: Test) {
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
    }

    compileIntegrationTestJava.options.compilerArgs << '-Xlint:unchecked'

    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    javadoc {
        options.links 'http://docs.oracle.com/javase/8/docs/api/'
        options.links 'http://docs.confluent.io/5.5.1/connect/javadocs/'
        options.links 'https://googleapis.dev/java/google-cloud-clients/0.97.0-alpha/'
        options.links 'https://kafka.apache.org/0100/javadoc/'
        options.links 'https://avro.apache.org/docs/1.8.1/api/java/'
    }

    jacocoTestReport {
        reports {
            html.destination file("${buildDir}/reports/jacoco/")
            xml.enabled true
        }
    }

    dependencies {
        compile (
                project(':kcbq-api'),

                "com.google.cloud:google-cloud-bigquery:$googleCloudVersion",
                "com.google.cloud:google-cloud-storage:$googleCloudVersion",
                "com.google.auth:google-auth-library-oauth2-http:$googleAuthVersion",
                "com.google.code.gson:gson:$googleCloudGsonVersion",
                "org.slf4j:slf4j-api:$slf4jVersion",
        )

        compile "io.debezium:debezium-core:$debeziumVersion", withoutKafka

        compileOnly (
                "org.apache.kafka:connect-api:$kafkaVersion"
        )

        testCompile (
                "junit:junit:$junitVersion",
                "org.mockito:mockito-core:$mockitoVersion",
                "org.mockito:mockito-inline:$mockitoVersion",
                "org.apache.kafka:kafka_$kafkaScalaVersion:$kafkaVersion",
                "org.apache.kafka:kafka_$kafkaScalaVersion:$kafkaVersion:test",
                "org.apache.kafka:kafka-clients:$kafkaVersion:test",
                "org.apache.kafka:connect-api:$kafkaVersion",
                "org.apache.kafka:connect-runtime:$kafkaVersion",
                "org.apache.kafka:connect-runtime:$kafkaVersion:test",
        )
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project {
                    name 'Kafka Connect BigQuery Connector'
                    packaging 'jar'
                    description 'A Kafka Connector used to load data into BigQuery'
                    url 'https://github.com/wepay/kafka-connect-bigquery'
                }
            }
        }
    }
}

project('kcbq-api') {
    jar {
        manifest {
            attributes  'Implementation-Title': 'Kafka Connect BigQuery API',
                        'Implementation-Version': version
        }
    }

    repositories {
        mavenCentral()
    }

    javadoc {
        options.links 'http://docs.oracle.com/javase/8/docs/api/'
        options.links 'http://docs.confluent.io/5.5.1/connect/javadocs/'
    }

    dependencies {
        compile "com.google.cloud:google-cloud-bigquery:$googleCloudVersion"

        compileOnly "org.apache.kafka:connect-api:$kafkaVersion"
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project {
                    name 'Kafka Connect BigQuery Connector API'
                    packaging 'jar'
                    description 'A small API for the Kafka Connector used to load data into BigQuery'
                    url 'https://github.com/wepay/kafka-connect-bigquery'
                }
            }
        }
    }
}

project('kcbq-confluent') {
    apply plugin: 'distribution'

    distributions {
        main {
            baseName = 'kcbq-confluent'
            contents {
                from configurations.runtime, jar
            }
        }
    }

    jar {
        manifest {
            attributes  'Implementation-Title': 'Kafka Connect BigQuery Schema Registry Schema Retriever',
                        'Implementation-Version': version
        }
    }

    repositories {
        mavenCentral()
        maven {
            url 'http://packages.confluent.io/maven'
        }
        jcenter()
    }

    javadoc {
        options.links 'http://docs.oracle.com/javase/8/docs/api/'
        options.links 'http://docs.confluent.io/5.5.1/connect/javadocs/'
    }

    dependencies {

        compile (
                project(':kcbq-connector'),
                project(':kcbq-api'),

                "org.apache.avro:avro:$avroVersion",
                "org.slf4j:slf4j-api:$slf4jVersion",
        )

        compile "io.confluent:kafka-connect-avro-converter:$ioConfluentVersion", withoutKafka
        compile "io.confluent:kafka-schema-registry-client:$ioConfluentVersion", withoutKafka

        compileOnly (
                "org.apache.kafka:connect-api:$kafkaVersion",
                "org.apache.kafka:kafka-clients:$kafkaVersion"
        )

        testCompile (
                "junit:junit:$junitVersion",
                "org.mockito:mockito-core:$mockitoVersion",
                "org.mockito:mockito-inline:$mockitoVersion",
                "org.apache.kafka:connect-api:$kafkaVersion",
                "org.apache.kafka:kafka-clients:$kafkaVersion"

        )
    }

    artifacts {
        archives javadocJar, sourcesJar, distTar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project {
                    name 'Kafka Connect BigQuery Connector Schema Registry Schema Retriever'
                    packaging 'jar'
                    description 'A Schema Registry-based schema retriever for the Kafka Connector used to load data into BigQuery'
                    url 'https://github.com/wepay/kafka-connect-bigquery'
                }
            }
        }
    }
}
// END INDIVIDUAL PROJECTS
