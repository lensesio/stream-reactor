// BEGIN ALL PROJECTS //
allprojects {
    apply plugin: 'java'
}

// END ALL PROJECTS

project.ext {
    apacheHttpClientVersion = '4.5.6'
    avroVersion = '1.8.1'
    debeziumVersion = '0.6.1'
    googleCloudVersion = '0.47.0-alpha'
    googleAuthVersion = '0.9.0'
    googleCloudGsonVersion = '2.8.5'
    ioConfluentVersion = '5.1.1'
    junitVersion = '4.12'
    kafkaVersion = '1.1.1'
    mockitoVersion = '1.10.19'
    slf4jVersion = '1.6.1'
}

// BEGIN SUBPROJECTS //
subprojects { subproject ->

    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    jar.baseName = subproject.name

    [compileJava, compileTestJava].each {
        it.options.compilerArgs << '-Xlint:unchecked'
    }

    checkstyle {
        configFile = file("${rootDir}/config/checkstyle/google_checks.xml")
        toolVersion = '6.18'
    }

    findbugsMain {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    signing {
        sign configurations.archives
        required {
            gradle.taskGraph.hasTask('uploadArchives')
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                    authentication(userName: findProperty('ossrhUsername') ?: '', password: findProperty('ossrhPassword') ?: '')
                }

                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                    authentication(userName: findProperty('ossrhUsername') ?: '', password: findProperty('ossrhPassword') ?: '')
                }

                pom.project {

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    scm {
                        connection 'scm:git:git://github.com/wepay/kafka-connect-bigquery.git'
                        developerConnection 'scm:git:ssh://github.com:wepay/kafka-connect-bigquery.git'
                        url 'https://github.com/wepay/kafka-connect-bigquery'
                    }

                    developers {
                        developer {
                            id 'C0urante'
                            name 'Chris Egerton'
                            email 'fearthecellos@gmail.comw'
                        }

                        developer {
                            id 'moirat'
                            name 'Moira Tagle'
                            email 'moirat@wepay.com'
                        }
                    }
                }
            }
        }
    }
}
// END SUBPROJECTS

// BEGIN INDIVIDUAL PROJECTS
project(':kcbq-connector') {
    apply plugin: 'jacoco'

    jar {
        manifest {
            attributes  'Implementation-Title': 'Kafka Connect BigQuery Connector',
                        'Implementation-Version': version

        }
    }

    repositories {
        mavenCentral()
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output
                runtimeClasspath += main.output
                srcDir file('src/integration-test/java')
            }
            resources.srcDir file('src/integration-test/resources')
        }
    }

    task integrationTestPrep() {
        dependsOn 'integrationTestTablePrep'
        dependsOn 'integrationTestBucketPrep'
    }

    task integrationTestTablePrep(type: JavaExec) {
        main = 'com.wepay.kafka.connect.bigquery.it.utils.TableClearer'
        classpath = sourceSets.integrationTest.runtimeClasspath
        args findProperty('kcbq_test_keyfile') ?: ''
        args findProperty('kcbq_test_project') ?: ''
        args findProperty('kcbq_test_dataset') ?: ''
        if (findProperty('kcbq_test_tables') != null)
            args findProperty('kcbq_test_tables').split(' ')
    }

    task integrationTestBucketPrep(type: JavaExec) {
        main = 'com.wepay.kafka.connect.bigquery.it.utils.BucketClearer'
        classpath = sourceSets.integrationTest.runtimeClasspath
        args findProperty('kcbq_test_keyfile') ?: ''
        args findProperty('kcbq_test_project') ?: ''
        args findProperty('kcbq_test_bucket') ?: ''
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
    }

    compileIntegrationTestJava.options.compilerArgs << '-Xlint:unchecked'

    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    javadoc {
        options.links 'http://docs.oracle.com/javase/8/docs/api/'
        options.links 'http://docs.confluent.io/3.2.0/connect/javadocs/'
        options.links 'http://googlecloudplatform.github.io/google-cloud-java/0.2.7/apidocs/'
        options.links 'https://kafka.apache.org/0100/javadoc/'
        options.links 'https://avro.apache.org/docs/1.8.1/api/java/'
    }

    jacocoTestReport {
        reports {
            html.destination "${buildDir}/reports/jacoco/"
            xml.enabled true
        }
    }

    dependencies {
        compile (
                project(':kcbq-api'),

                "com.google.cloud:google-cloud:$googleCloudVersion",
                "com.google.auth:google-auth-library-oauth2-http:$googleAuthVersion",
                "com.google.code.gson:gson:$googleCloudVersion",
                "io.debezium:debezium-core:$debeziumVersion",
                "org.apache.kafka:connect-api:$kafkaVersion",
                "org.apache.kafka:kafka-clients:$kafkaVersion",
                "org.slf4j:slf4j-api:$slf4jVersion",
                "org.slf4j:slf4j-simple:$slf4jVersion",
        )

        testCompile (
                "junit:junit:$junitVersion",
                "org.mockito:mockito-core:$mockitoVersion"
        )
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project {
                    name 'Kafka Connect BigQuery Connector'
                    packaging 'jar'
                    description 'A Kafka Connector used to load data into BigQuery'
                    url 'https://github.com/wepay/kafka-connect-bigquery'
                }
            }
        }
    }
}

project('kcbq-api') {
    jar {
        manifest {
            attributes  'Implementation-Title': 'Kafka Connect BigQuery API',
                        'Implementation-Version': version
        }
    }

    repositories {
        mavenCentral()
    }

    javadoc {
        options.links 'http://docs.oracle.com/javase/8/docs/api/'
        options.links 'http://docs.confluent.io/3.2.0/connect/javadocs/'
    }

    dependencies {
        compile (
                "com.google.cloud:google-cloud:$googleCloudVersion",
                "org.apache.kafka:connect-api:$kafkaVersion"
        )
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project {
                    name 'Kafka Connect BigQuery Connector API'
                    packaging 'jar'
                    description 'A small API for the Kafka Connector used to load data into BigQuery'
                    url 'https://github.com/wepay/kafka-connect-bigquery'
                }
            }
        }
    }
}

project('kcbq-confluent') {
    apply plugin: 'distribution'

    configurations.all {
        resolutionStrategy {
            // depending on debezium results in us pulling in the most recent kafka version, but we want to use 1.1.1.
            force 'org.apache.kafka:connect-api:1.1.1', 'org.apache.kafka:kafka-clients:1.1.1'
        }
    }

    distributions {
        main {
            baseName = 'kcbq-confluent'
            contents {
                from configurations.runtime, jar
            }
        }
    }

    jar {
        manifest {
            attributes  'Implementation-Title': 'Kafka Connect BigQuery Schema Registry Schema Retriever',
                        'Implementation-Version': version
        }
    }

    repositories {
        mavenCentral()
        maven {
            url 'http://packages.confluent.io/maven'
        }
        jcenter()
    }

    javadoc {
        options.links 'http://docs.oracle.com/javase/8/docs/api/'
        options.links 'http://docs.confluent.io/3.2.0/connect/javadocs/'
    }

    dependencies {
        runtime project(':kcbq-connector')

        compile (
                project(':kcbq-api'),

                "com.google.cloud:google-cloud:$googleCloudVersion",
                "io.confluent:kafka-connect-avro-converter:$ioConfluentVersion",
                "io.confluent:kafka-schema-registry-client:$ioConfluentVersion",
                "org.apache.avro:avro:$avroVersion",
                "org.apache.kafka:connect-api:$kafkaVersion",
                "org.apache.kafka:kafka-clients:$kafkaVersion",
                "org.slf4j:slf4j-api:$slf4jVersion",
        )

        testCompile (
                "junit:junit:$junitVersion",
                "org.mockito:mockito-core:$mockitoVersion"
        )
    }

    artifacts {
        archives javadocJar, sourcesJar, distTar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project {
                    name 'Kafka Connect BigQuery Connector Schema Registry Schema Retriever'
                    packaging 'jar'
                    description 'A Schema Registry-based schema retriever for the Kafka Connector used to load data into BigQuery'
                    url 'https://github.com/wepay/kafka-connect-bigquery'
                }
            }
        }
    }
}
// END INDIVIDUAL PROJECTS
